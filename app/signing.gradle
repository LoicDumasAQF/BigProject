/** You need to define in  ~/.gradle/gradle.properties a AndroidProject.signing = path to a file which contain :
 *  STORE_PASSWORD= keyStoreStorePassword
 *  KEY_PASSWORD= keyStoreKeyPassWard
 *  KEY_ALIAS= keyStoreAlias
 *  STORE_FILE= keyStorePath
 */

if (project.hasProperty("AndroidProject.signing") && new File(project.property("AndroidProject.signing").toString()).exists()) {
    Properties props = new Properties()
    def propFile = new File(project.property("AndroidProject.signing").toString())
    if (propFile.canRead()) {
        props.load(new FileInputStream(propFile))
        if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
                props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
            android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
            android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
            android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
            android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
        } else {
            println 'WARNING : androidproject.properties found but some entries are missing.\n' +
                    'Need properties : STORE_FILE, STORE_PASSWORD, KEY_ALIAS and KEY_PASSWORD'
            android.buildTypes.release.signingConfig = null
        }
    } else {
        println 'WARNING : androidproject.properties file not found.\n' +
                'Need properties : STORE_FILE, STORE_PASSWORD, KEY_ALIAS and KEY_PASSWORD'
        android.buildTypes.release.signingConfig = null
    }
}
